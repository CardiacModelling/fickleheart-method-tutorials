[[model]]
author: Chon Lok Lei
name: model_C
desc: """
Myokit version of model C: Mazhari et al. 2001 model.
"""
# Initial values
ikr.C3 = 1
ikr.C2 = 0
ikr.C1 = 0
ikr.O = 0

#
# Simulation engine variables
#
[engine]
time = 0 bind time
pace = 0 bind pace

#
# Membrane potential
#
[membrane]
V = engine.pace
    desc: membrane potential
    in [mV]

#
# Model
#
[ikr]
use membrane.V
IKr = g * O * (V - rev.EK)
I = 1 - (O + C1 + C2 + C3)
a1 = p1 * exp( p2 * V)
b1 = p3 * exp(-p4 * V)
a2 = p7 * exp( p8 * V)
b2 = p9 * exp(-p10 * V)
a3 = p5
b3 = p6
a4 = p11 * exp( p12 * V)
b4 = p13 * exp(-p14 * V)
a5 = p15 * exp( p16 * V)
b5 = (b1 * b2  * a5) / (a1 * a2)  # microscopic reversibility condition

dot(C1) = b1 * O + a3 * C2 + b5 * I - (a1 + b3 + a5) * C1
dot(C2) = b3 * C1 + a4 * C3 - (a3 + b4) * C2
dot(C3) = b4 * C2 - a4 * C3
dot(O) = b2 * I + a1 * C1 - (a2 + b1) * O

p1 = 0.0218 [1/ms]
p2 = 0.0262 [1/mV]
p3 = 0.0009 [1/ms]
p4 = 0.0269 [1/mV]
p5 = 0.0266 [1/ms]
p6 = 0.1348 [1/ms]
p7 = 0.0622 [1/ms]
p8 = 0.0120 [1/mV]
p9 = 0.0059 [1/ms]
p10 = 0.0443 [1/mV]
p11 = 0.0069 [1/ms]
p12 = 0.0272 [1/mV]
p13 = 0.0227 [1/ms]
p14 = 0.0431 [1/mV]
p15 = 0.0000129 [1/ms]
p16 = 0.00000271 [1/mV]
g = 0.1524 * 1e3 [pA/mV]

n_params = 17


#
# Potassium concentrations
#
[potassium]
Ki = 110 [mM]
Ko = 4 [mM]

#
# Reversal parameters
#
[rev]
use phys.RTF
EK = RTF * log(potassium.Ko / potassium.Ki)
    in [V]


#
# Physical constants
#
[phys]
R = 8.314472 [J/mol/K]
#T = 310 [K]  # 36-37oC (BT)
T = 297 [K]  # 24oC (RT)
F = 9.64853415e4 [C/mol]
RTF = R * T / F * 1e3 in [mV]
